import pandas
from Bio.SeqIO import parse
import os

FILES1 = glob_wildcards( 'data/hmms-enabled/{hmm}.hmm' )
FILES2 = glob_wildcards( 'data/genomes-enabled/{fasta}.fna' )

# extract the {hmm} values into a list
HMMS    = FILES1.hmm
GENOMES = FILES2.fasta

# borrowed from CheckV
def parse_hmmsearch( path ) :
  with open(path) as f:
    names = [ "qname", "qacc", "tname", "tacc",   "eval", 
              "score", "bias", "beval", "bscore", "bbias" ]
    formats = [ str,   str,   str,   str,   float, 
                float, float, float, float, float ]
    for line in f:
      if not line.startswith("#") :
        values = line.split()
        yield dict( [ ( names[i], formats[i](values[i]) ) 
                      for i in range(10) ] )

rule all :
  input :
    # use the extracted name values to build new filenames
    #expand( 'proteins/{fasta}_genes.faa',
    #        fasta=GENOMES ),
    #expand( 'hmm_hits/{fasta}_{hmm}_prot.txt',
    #        hmm=HMMS, fasta=GENOMES ),
    #expand( 'prealignments/{hmm}.faa',
    #        hmm=HMMS ),
    expand( 'alignments/{hmm}.msa.faa',
            hmm=HMMS )

rule clean :
  shell :
    'rm -rf proteins hmm_hits prealignments logs'

rule prodigal:
  input :
    fna='data/genomes-enabled/{fasta}.fna', 
  output :
    faa='proteins/{fasta}_genes.faa',
    #fna='proteins/{fasta}_genes.fna',
    #gff='proteins/{fasta}_genes.gff',
  log :
    'logs/prodigal/{fasta}.log',
  params :
    extra='',  # Additional arguments
  wrapper :
    'file://wrappers/prodigal-gv'

rule hmmer :
  input :
    fasta   = 'proteins/{fasta}_genes.faa',
    profile = 'data/hmms-enabled/{hmm}.hmm'
  log :
    'logs/hmmer/{fasta}_{hmm}.log',
  output :
    tblout         = 'hmm_hits/{fasta}_{hmm}_prot_tbl.txt',
    domtblout      = 'hmm_hits/{fasta}_{hmm}_prot_dom_tbl.txt',
    alignment_hits = 'hmm_hits/{fasta}_{hmm}_alg_hits.txt',
    outfile        = 'hmm_hits/{fasta}_{hmm}_prot.txt'
  wrapper :
    'file://wrappers/hmmer/hmmsearch'

rule scoring :
  input :
    # for each HMM...
    hits = expand( 'hmm_hits/{fasta}_{{hmm}}_prot_tbl.txt',
                   fasta=GENOMES ),
    # ...score orthologs for each genome
    faa  = expand( 'proteins/{fasta}_genes.faa',
                   fasta=GENOMES )
  output :
    prealign = 'prealignments/{hmm}.faa'
  run :
    nohits = 0
    for tblout,faa in zip( input.hits, input.faa ) :
      hits = pandas.DataFrame( [ h for h in parse_hmmsearch( tblout ) ] )
      if hits.shape[0] == 0 :
        nohits += 1
        continue
      top_hit = hits.sort_values( 'bscore', ascending=False ).iloc[0]
      with open( output.prealign, 'a' ) as f :
        for rec in parse( open( faa ), 'fasta' ) :
          if rec.id == top_hit['qname'] : 
            f.write( rec.format( 'fasta' ) )
            break
    if nohits == len( input.hits ) :
      print( 'writing empty file for {a}...'.format( a=output.prealign ) )
      with open( output.prealign, 'w' ) as f :
        f.write( '' )

rule clustalo :
    input :
        'prealignments/{hmm}.faa'
    output :
        'alignments/{hmm}.msa.faa'
    params:
        extra=''
    log :
        'logs/clustalo/{hmm}.log'
    threads : 8
    wrapper :
      'file://wrappers/clustalo'
